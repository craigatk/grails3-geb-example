buildscript {
    ext {
        grailsVersion = project.grailsVersion
    }
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath 'com.bertramlabs.plugins:asset-pipeline-gradle:2.1.1'
    }
}

plugins {
    id "io.spring.dependency-management" version "0.5.1.RELEASE"
}

version "0.1"
group "grails3.geb.example"

apply plugin: "spring-boot"
apply plugin: "war"
apply plugin: "asset-pipeline"
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: "org.grails.grails-web"
apply plugin: "org.grails.grails-gsp"

ext {
    grailsVersion = project.grailsVersion
    gradleWrapperVersion = project.gradleWrapperVersion
}

assets {
    minifyJs = true
    minifyCss = true
}

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-dependencies"
    compile "org.grails:grails-web-boot"

    compile "org.grails.plugins:hibernate"
    compile "org.grails.plugins:cache"
    compile "org.hibernate:hibernate-ehcache"

    runtime "org.grails.plugins:asset-pipeline"
    runtime "org.grails.plugins:scaffolding"

    testCompile "org.grails:grails-plugin-testing"
    testCompile "org.grails.plugins:geb"
    testCompile "org.gebish:geb-core:0.13.1"

    testCompile "org.seleniumhq.selenium:selenium-support:${project.seleniumVersion}"
    testCompile "org.seleniumhq.selenium:selenium-chrome-driver:${project.seleniumVersion}"
    testCompile "org.seleniumhq.selenium:selenium-firefox-driver:${project.seleniumVersion}"
    testCompile "org.seleniumhq.selenium:selenium-ie-driver:${project.seleniumVersion}"

    // For downloading browser-specific drivers that browsers like Chrome and IE require
    testCompile("io.github.bonigarcia:webdrivermanager:1.4.1") {
        exclude group: 'org.seleniumhq.selenium'
    }

    console "org.grails:grails-console"
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}

// Pass system properties through to the integrationTest task so we can pass in the 'geb.env' property to run tests
// in different browsers. Adapted from http://mrhaki.blogspot.com/2015/09/grails-goodness-passing-system.html
configure(integrationTest) {
    systemProperties System.properties
}
